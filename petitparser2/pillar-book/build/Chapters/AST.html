<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="UTF-8">
    <title>Parsing with PetitParser2</title>
    <meta name="viewport" content="width=device-width, initial-scale=1.0">

    <!-- Highlight js -->
    <link rel="stylesheet" href="../support/html/highlightjs/styles/default.css">
    <link rel="stylesheet" href="../support/html/css/highlight-commands.css">
    <script src="../support/html/highlightjs/highlight.pack.js"></script>
    <script src="../support/html/js/highlight-commands.js"></script>

    <!-- Bootstrap -->
    <link href="../support/html/css/bootstrap.min.css" rel="stylesheet">
    <link href="../support/html/css/bootstrap-theme.min.css" rel="stylesheet">
    <link href="../support/html/css/square-braket-associates.css" rel="stylesheet">
    <style>
/*        .container { max-width: 55em; } */
    </style>

    <!-- HTML5 Shim and Respond.js IE8 support of HTML5 elements and media queries -->
    <!-- WARNING: Respond.js doesn't work if you view the page via file:// -->
    <!--[if lt IE 9]>
      <script src="https://oss.maxcdn.com/libs/html5shiv/3.7.0/html5shiv.js"></script>
      <script src="https://oss.maxcdn.com/libs/respond.js/1.3.0/respond.min.js"></script>
    <![endif]-->
  </head>
  <body>
    <div class="container text-justify">
      <section>
        <h1>Parsing with PetitParser2</h1>
        <div class="attribution"></div>

        <a id="top"></a>
<p>
This text is part of the <a href="Chapters/../../book.html">Parsing With PetitParser2</a> series.
The <a href="#toc">table of content</a> can be found at the end of the chapter.

</p>
<section>
<h1>Create an Abstract Syntax Tree</h1>

<p>
In the previous chapter we tested that our grammar parses the nested elements,
but we have very limited idea how does the extracted structure look like.
The direct output of a parser is typically very detailed and not well-readable. 
It is called concrete syntax tree (CST), which is (in the case of PetitParser) array of arrays of arrays of ... with characters and strings as terminals.
</p>
<p>
It is the time to return a more convenient representation of the input: <em>an abstract syntax tree</em>, in short AST. 
Abstract syntax tree, contrary to the very low-level and detailed concrete syntax tree, is conceptually closer to the target domain. 
In our case the domain should be some kind of a tree with html elements and javascript.
</p>

<p>
It is considered to be a good practice in PetitParser to split grammar, which returns concrete syntax tree, from parser, which returns abstract syntax tree. 
We do this by subclassing <code>WebGrammar</code>:
</p>
<figure><pre><code class="smalltalk">WebGrammar subclass: #WebParser
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PetitParser2-Tutorial'</code></pre><figcaption></figcaption></figure>

<section>
<h2>1. Testing First</h2>

<p>
Parsing is great use case for test driven development. 
So let us start with tests first. 
Tests give us a clear idea what kind of interface we expect from abstract syntax tree:
</p>
<figure><pre><code class="smalltalk">WebGrammarTest subclass: #WebParserTest
	uses: TPP2TypeAssertions
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PetitParser2-Tutorial'

WebParserTest&gt;&gt;parserClass
	^ WebParser

WebParserTest&gt;&gt;testElement
	super testElement.
	
	self assert: result name equals: 'p'.
	self assert: result firstChild text equals: 'lorem ipsum'

WebParserTest&gt;&gt;testElementEmpty
	super testElementEmpty.
	
	self assert: result name equals: 'foo'.

WebParserTest&gt;&gt;testElementNested
	super testElementNested.
	
	self assert: result name equals: 'p'.
	self assert: result firstChild text trim equals: 'lorem'.
	self assert: result secondChild name equals: 'i'.
	self assert: result secondChild firstChild text equals: 'ipsum'</code></pre><figcaption></figcaption></figure>


<p>
And for malformed elements, we expect the following results:
</p>
<figure><pre><code class="smalltalk">WebParserTest&gt;&gt;testElementMalformedExtraClose
	super testElementMalformedExtraClose.
	
	self assert: result name equals: 'foo'.
	self assert: result secondChild text equals: '&lt;/fii&gt;'

WebParserTest&gt;&gt;testElementMalformedWrongClose
	super testElementMalformedWrongClose.
	
	self assert: result name equals: 'foo'.
	self assert: result firstChild text equals: '&lt;bar&gt;meh&lt;/baz&gt;'

WebParserTest&gt;&gt;testElementMalformedUnclosed
	super testElementMalformedUnclosed.
	
	self assert: result name equals: 'head'.
	self assert: result firstChild text trim equals: '&lt;meta content=&quot;mess&quot;&gt;'</code></pre><figcaption></figcaption></figure>


<p>
And of course, we expect javascript code to be extracted as follows:
</p>
<figure><pre><code class="smalltalk">WebParserTest&gt;&gt;testJavascript
	super testJavascript.
	
	self assert: result code equals: 'alert(&quot;hi there!&quot;)'

WebParserTest&gt;&gt;testJavascriptWithString
	super testJavascriptWithString.
	
	self assert: result code equals: 'alert(''&lt;/script&gt;'')'</code></pre><figcaption></figcaption></figure>

</section><section>
<h2>2. AST Nodes</h2>
<p>
If we want to pass these test, the result of a parse should be a tree consisting of three different nodes: 
</p><ol><li>a javascript code; </li>
<li>an html element; and</li>
<li>a text.</li>
</ol>

<p>
We define these nodes as follows, starting with its abstract predecessor <code>WebElement</code>, which contains methods that ease our testing.
</p>
<figure><pre><code class="smalltalk">Object subclass: #WebElement
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PetitParser2-Tutorial'

WebElement&gt;&gt;children
	^ #()

WebElement&gt;&gt;firstChild
	&quot;Just for convenience&quot;
	^ self children first

WebElement&gt;&gt;secondChild
	&quot;Just for convenience&quot;
	^ self children second

WebElement&gt;&gt;gtTreeViewIn: composite
	&lt;gtInspectorPresentationOrder: 40&gt;

	composite tree
		title: 'Tree';
		children: [:n | n children ];
		format: [:n| n displayText printStringLimitedTo: 50 ];
		shouldExpandToLevel: 6</code></pre><figcaption></figcaption></figure>

<p>
<code>JavascriptElement</code> follows:
</p>
<figure><pre><code class="smalltalk">WebElement subclass: #JavascriptElement
	instanceVariableNames: 'code'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PetitParser2-Tutorial'

JavascriptElement&gt;&gt;code
	^ code

JavascriptElement&gt;&gt;code: anObject
	code := anObject

JavascriptElement&gt;&gt;displayText
	^ self code

JavascriptElement&gt;&gt;gtText: composite
	&lt;gtInspectorPresentationOrder: 40&gt;
	
	composite text
		title: 'Text';
		display: [ :context | code ]</code></pre><figcaption></figcaption></figure>

<p>
As well as <code>HtmlElement</code>:
</p>
<figure><pre><code class="smalltalk">WebElement subclass: #HtmlElement
	instanceVariableNames: 'name children'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PetitParser2-Tutorial'

HtmlElement&gt;&gt;children
	^ children

HtmlElement&gt;&gt;name
	^ name

HtmlElement&gt;&gt;name: newName
	name := newName

HtmlElement&gt;&gt;displayText
	^ self name</code></pre><figcaption></figcaption></figure>


<p>
Last but not least <code>UnknownText</code>:
</p>
<figure><pre><code class="smalltalk">WebElement subclass: #UnknownText
	instanceVariableNames: 'text'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'PetitParser2-Tutorial'

UnknownText&gt;&gt;text
	^ text

UnknownText&gt;&gt;text: anObject
	text := anObject

UnknownText&gt;&gt;gtText: composite
	&lt;gtInspectorPresentationOrder: 40&gt;
	
	composite text
		title: 'Text';
		display: [ :context | text ]</code></pre><figcaption></figcaption></figure>



</section><section>
<h2>3. From Grammar to Parser</h2>

<p>
Now we override the rules of <code>WebGrammar</code> in <code>WebParser</code> so that the <code>javascript</code> rule returns <code>JavascriptElement</code>, the <code>element</code> rule returns <code>HtmlElement</code> and the <code>text</code> rule returns <code>UnknownText</code>:
</p>

<figure><pre><code class="smalltalk">WebParser&gt;&gt;javascript
	^ super javascript
	
	map: [ :_code | 
		(JavascriptElement new)
			code: _code;
			yourself
	]

WebParser&gt;&gt;element
	^ super element 
	
	map: [ :_open :_content :_close | 
	 	(HtmlElement new)
			name: _open;
			children: _content;
			yourself
	]

WebParser&gt;&gt;text
	^ super text flatten
	
	map: [ :_value | 
		UnknownText new
			text: _value;
			yourself	
	]</code></pre><figcaption></figcaption></figure>

<p>
And finally, for convenience, we trim whitespaces around html elements:
</p>
<figure><pre><code class="smalltalk">WebParser&gt;&gt;elClose
	^ super elClose trim

WebParser&gt;&gt;elOpen
	^ super elOpen trimRight</code></pre><figcaption></figcaption></figure>

<p class="todo">explain there is <code>trimRight</code> so that the caching is more efficient</p>
<p>
By this time all the tests should pass:
</p>
<figure><pre><code class="smalltalk">8 run, 8 passes, 0 skipped, 0 expected failures, 0 failures, 0 errors, 0 unexpected passes</code></pre><figcaption></figcaption></figure>

</section><section>
<h2>4. Structured Document</h2>

<p>
Now the whole HTML document.
The pieces should be there, we need to connect them.
Of course, we write tests first:
</p>

<figure><pre><code class="smalltalk">WebGrammarTest&gt;&gt;testStructuredDocumentSimple
	| input |
	input := '&lt;html&gt;
		&lt;body&gt;
			&lt;script&gt;alert(&quot;hello world&quot;)&lt;/script&gt;
		&lt;/body&gt;
	&lt;/html&gt;'.
	
	self parse: input rule: #structuredDocument

WebGrammarTest&gt;&gt;testStructuredDocumentWithDoctype
	| input |
	input := '
&lt;!DOCTYPE html&gt;
&lt;!-- comment --&gt;
&lt;html&gt;
	&lt;body&gt;
		&lt;script&gt;alert(&quot;hello world&quot;)&lt;/script&gt;
	&lt;/body&gt;
&lt;/html&gt;'.
	
	self parse: input rule: #structuredDocument

WebGrammarTest&gt;&gt;testStructuredDocument
	| input |
	input := PP2Sources current htmlSample.
	
	self parse: input rule: #structuredDocument</code></pre><figcaption></figcaption></figure>

<p>
In the previous chapters, we defined document (represented by the <code>document</code> rule) in <code>WebGrammar</code> as a repetition of javascript seas. 
Now we define a structured document, i.e. a document with an html structure. 
The root of an html file is an element that can be surrounded by some other information (e.g. doctype or comments), therefore we define it as sea:
</p>
<figure><pre><code class="smalltalk">WebGrammar&gt;&gt;structuredDocument
	^ element sea</code></pre><figcaption></figcaption></figure>


<p>
In order to verify the structure we extract from a document, we write the tests for parser:
</p>
<figure><pre><code class="smalltalk">WebParserTest&gt;&gt;testStructuredDocumentSimple
	| html body javascript |
	super testStructuredDocumentSimple.
	
	self assert: result name equals: 'DOCUMENT'.


	html := result secondChild.
	self assert: html name equals: 'html'.

	body := html firstChild.
	self assert: body name equals: 'body'.
	
	javascript := body firstChild.
	self assert: javascript isKindOf: JavascriptElement.
	self assert: javascript code equals: 'alert(&quot;hello world&quot;)'.

WebParserTest&gt;&gt;testStructuredDocumentWithDoctype
	| html body javascript |
	super testStructuredDocumentWithDoctype.
	
	self assert: result name equals: 'DOCUMENT'.


	html := result secondChild.
	self assert: html name equals: 'html'.

	body := html firstChild.
	self assert: body name equals: 'body'.
	
	javascript := body firstChild.
	self assert: javascript isKindOf: JavascriptElement.
	self assert: javascript code equals: 'alert(&quot;hello world&quot;)'.


WebParserTest&gt;&gt;testStructuredDocument
	| html |
	super testStructuredDocument.
	
	self assert: result name equals: 'DOCUMENT'.


	html := result secondChild.
	self assert: html name equals: 'html'.


	self assert: html firstChild name equals: 'head'.	
	self assert: html secondChild name equals: 'body'.</code></pre><figcaption></figcaption></figure>


<p>
In order to pass these tests, we create a root element from the structured document:
</p>
<figure><pre><code class="smalltalk">WebParser&gt;&gt;structuredDocument
	^ super structuredDocument
	
	map: [ :_bw :_document :_aw |
		| beforeWater afterWater |
		beforeWater := UnknownText new
			text: _bw;
			yourself.
			
		afterWater := UnknownText new
			text: _aw;
			yourself.
			
		HtmlElement new
			name: 'DOCUMENT';
			children: (Array with: beforeWater 
					 with: _document 
					 with: afterWater);
			yourself
	
	]</code></pre><figcaption></figcaption></figure>

<p>
By this time all the tests should pass:
</p><figure><pre><code class="smalltalk">11 run, 11 passes, 0 skipped, 0 expected failures, 0 failures, 0 errors, 0 unexpected passes</code></pre><figcaption></figcaption></figure>

</section><section>
<h2>5. Comments</h2>

<p>
In this section we focus a bit more how to handle comments. They may contain scripts or other elements and they may confuse the parser. 
</p>
<p>
Actually we have been cheating a bit and there is an error in <code>WebParser</code>, which extracts one element that is not in the HTML. Inspect the following and switch to the tree view:
</p>
<figure><pre><code class="smalltalk">WebParser new parse: input.</code></pre><figcaption></figcaption></figure>

<p>
There is a <em>&lt;p&gt;</em> element in the <em>&lt;body&gt;</em>. 
But <em>&lt;p&gt;</em> is a part of comment, it should not be included in the document structure. 
We fix the <code>testStructuredDocument</code> to cover this:
</p>
<figure><pre><code class="smalltalk">WebParserTest&gt;&gt;testStructuredDocument
	| html body |
	super testStructuredDocument.
	
	self assert: result name equals: 'DOCUMENT'.

	html := result secondChild.
	self assert: html name equals: 'html'.

	self assert: html firstChild name equals: 'head'.	
	self assert: html secondChild name equals: 'body'.
	
	body := html secondChild.
	self assert: body children size equals: 4.</code></pre><figcaption></figcaption></figure>

<p>
The problem is tha same as in the case of prematurely ended javascript.
<code>WebGrammar</code> has no notion of a comment and handles a content of comment as an ordinary html code. 
Therefore we define what is an html comment:
</p>

<figure><pre><code class="smalltalk">WebGrammar&gt;&gt;comment
	^ '&lt;!--' asPParser, #any asPParser starLazy, '--&gt;' asPParser</code></pre><figcaption></figcaption></figure>

<p>
And now we can redefine <code>text</code> so that it knows what is a comment:
</p>
<figure><pre><code class="smalltalk">WebGrammar&gt;&gt;text
	^ (comment / #any asPParser) starLazy</code></pre><figcaption></figcaption></figure>


<p>
We already mentioned in the previous text that <code>starLazy</code> utilizes bounded seas and the above code is equivalent to:
</p>
<figure><pre><code class="smalltalk">(#epsilon asPParser)
  waterToken: (comment / #any asPParser);
  yourself	 </code></pre><figcaption></figcaption></figure>


<p>
Because now water knows what is a comment, it skips the complete comment including the <em>&lt;p&gt;</em> element inside.
</p>

<p>
This is it, we can parse all of it!
</p>
</section><section>
<h2>6. Conclusion </h2>
<p>
In this chapter we have applied the test-driven approach to define our AST nodes.
We defined <code>WebParser</code>, wchid extends the <code>WebGrammar</code> and builds the AST nodes.
Last but not least, we had a look how to avoid false positives caused by comments.
</p>


<section>
<h3>Complete Sources</h3>
<p>
You can download the sources here:
</p><ul>
<li><a href="../WebGrammar.st"><code>WebGrammar</code></a> </li>
<li><a href="../WebGrammarTest.st"><code>WebGrammarTest</code></a></li>
<li><a href="../WebParser.st"><code>WebParser</code></a> </li>
<li><a href="../WebParserTest.st"><code>WebParserTest</code></a></li>
<li><a href="../WebElement.st"><code>WebElement</code></a></li>
<li><a href="../JavascriptElement.st"><code>JavascriptElement</code></a></li>
<li><a href="../HtmlElement.st"><code>HtmlElement</code></a></li>
<li><a href="../UnknownText.st"><code>UnknownText</code></a></li>
</ul>

<p>
The source for this tutorial is also part of the PetitParser2 package.
</p>
<a id="toc"></a><section>
<h1>Table of Contents</h1>
<p>
This text is part of the <a href="Chapters/../../book.html">Parsing With PetitParser2</a> series.
</p>
<p>
Part I, Basics:
</p><ul>
<li><a href="Chapters/../chapter1.html">Extracting Javascript</a></li>
<li><a href="Chapters/../chapter2.html">From a Script to a Parser</a></li>
<li><a href="Chapters/../chapter3.html">Extracting the Structure</a></li>
<li><a href="Chapters/../AST.html">Abstract Syntax Tree</a></li>
<li><a href="Chapters/../optimizations.html">Optimizations</a></li>
<li><a href="Chapters/../memoization.html">Memoization</a></li>
</ul>

<p>
Part II, Advanced Topics:
</p><ul>
<li><a href="Chapters/../starLazy.html">The <code>starLazy</code> Operator</a></li>
<li><a href="Chapters/../matchingTags.html">Matching Tags</a></li>
<li><a href="Chapters/../smalltalkOptimization.html">Specializations in Detail</a></li>
<li><a href="Chapters/../caches.html">Caches in Detail</a></li>
</ul>

<p>
Go to <a href="#top">top</a>.
</p></section></section></section></section>
      </section>
    </div>

    <!-- jQuery (necessary for Bootstrap's JavaScript plugins) -->
    <script src="https://code.jquery.com/jquery.js"></script>
    <!-- Include all compiled plugins (below), or include individual files as needed -->
    <script src="../support/html/js/bootstrap.min.js"></script>

    <!-- Syntax highlighting of code blocks -->
    <script>hljs.initHighlightingOnLoad();</script>

    <!-- Prettify annotated paragraphs-->
    <script src="../support/html/js/annotated-paragraphs.js"></script>

  </body>
</html>
